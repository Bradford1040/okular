@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(Qt5Core @QT_REQUIRED_VERSION@)
find_dependency(Qt5PrintSupport @QT_REQUIRED_VERSION@)
find_dependency(Qt5Widgets @QT_REQUIRED_VERSION@)
find_dependency(KF5CoreAddons @KF5_REQUIRED_VERSION@)
find_dependency(KF5Config @KF5_REQUIRED_VERSION@)
find_dependency(KF5XmlGui @KF5_REQUIRED_VERSION@)

####################################################################################
# CMAKE_AUTOMOC

if(NOT CMAKE_VERSION VERSION_LESS "3.10.0")
    # CMake 3.9+ warns about automoc on files without Q_OBJECT, and doesn't know about other macros.
    # 3.10+ lets us provide more macro names that require automoc.
    list(APPEND CMAKE_AUTOMOC_MACRO_NAMES OKULAR_EXPORT_PLUGIN)
endif()

if(NOT CMAKE_VERSION VERSION_LESS "3.9.0")
    # CMake's automoc needs help to find names of plugin metadata files in case Q_PLUGIN_METADATA
    # is indirectly used via other C++ preprocessor macros
    # 3.9+ lets us provide some filter rule pairs (keyword, regexp) to match the names of such files
    # in the plain text of the sources. See AUTOMOC_DEPEND_FILTERS docs for details.
    list(APPEND CMAKE_AUTOMOC_DEPEND_FILTERS
        "OKULAR_EXPORT_PLUGIN"
        "[\n^][ \t]*OKULAR_EXPORT_PLUGIN[ \t\n]*\\([^,]*,[ \t\n]*\"([^\"]+)\""
    )
endif()

include("${CMAKE_CURRENT_LIST_DIR}/Okular5Targets.cmake")
